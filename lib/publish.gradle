apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '1.0.0'

publishing {
    publications {
        Production(MavenPublication) {
            println "$buildDir"
            artifact("$buildDir/outputs/aar/lib-release.aar")
            groupId 'com.walmartlabs.ern'
            artifactId 'android-showcaseapp-ern-container'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayApiKey
    publications = ['Production']
    configurations = ['archives']
    override = false
    pkg {
        repo = bintrayRepo
        name = 'electrode-native-android-showcase-app-container'
        description = "Container that holds the MiniApps and APIs for the ElectrodeNativeShowcaseApp for android."
        publish = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/deepueg/electrode-native-showcaseapp-android-container.git'
        dryRun = false
        publicDownloadNumbers = true
        version {
            name = this.version
            desc = "electrode-native-android-showcase-app-container ${this.version}"
            released  = new Date()
            vcsTag = this.version
        }
    }
}
